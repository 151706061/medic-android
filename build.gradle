buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.2.3'
	}
}

apply plugin: 'android'

repositories {
	mavenLocal()
	mavenCentral()
}

def isReleaseBuild = {
	return System.env.JENKINS == 'true' || (System.env.TRAVIS == 'true' &&
			System.env.TRAVIS_BRANCH == 'master')
}
def getVersionCode = {
	if(isReleaseBuild()) {
		def code = System.env.TRAVIS == 'true' ?
			System.env.TRAVIS_BUILD_NUMBER :
			System.env.VERSION_TO_BUILD.split(/\./)[-1]
		return Integer.parseInt(code)
	}
	return 1
}
def getVersionName = {
	if(isReleaseBuild()) {
		def v = System.env.MEDIC_VERSION_NAME
		if(!v) throw new RuntimeException(
				'Missing required env var: MEDIC_VERSION_NAME')
		return v
	}
	return 'SNAPSHOT'
}

android {
	compileSdkVersion 22
	buildToolsVersion '22.0.1'
	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}

	defaultConfig {
		versionCode getVersionCode()
		versionName getVersionName()
		archivesBaseName = "${project.name}-${versionName}"
	}

	applicationVariants.all {
		buildConfigField "String", "ICON_CHANGER", "\"${System.env.ICON_CHANGER}\"";
	}

	signingConfigs {
		release {
			storeFile file(System.env.ANDROID_KEYSTORE_PATH ?: signingConfigs.debug.storeFile)
			storePassword System.env.ANDROID_KEYSTORE_PASSWORD
			keyAlias System.env.ANDROID_KEY_ALIAS
			keyPassword System.env.ANDROID_KEY_PASSWORD
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFile getDefaultProguardFile('proguard-android.txt')
		}
	}

	productFlavors {
		unbranded {
			// we will not create project-specific src directories
			// for `unbranded` - it will use the defaults in
			// src/main
		}
		medicmobiledemo {
			applicationId = 'org.medicmobile.webapp.mobile.medicmobiledemo'
		}
		livinggoods {
			applicationId = 'org.medicmobile.webapp.mobile.livinggoods'
		}
	}
}
